plugins {
    id 'java'
    id "checkstyle"
    id 'jacoco'
    id 'com.adarshr.test-logger' version '3.2.0'
}

group 'com.nashss.se.musicplaylistservice'
version '1.0.0'
defaultTasks 'clean', 'build'

repositories {
    mavenCentral()
    maven {
        url = uri("https://maven.pkg.github.com/NSS-Software-Engineering/shared-java")
        credentials {
            username = project.findProperty('GH_PACKAGE_REG_READ_USER') ?: System.getenv('GH_PACKAGE_REG_READ_USER')
            password = project.findProperty('GH_PACKAGE_REG_READ_PASS') ?: System.getenv('GH_PACKAGE_REG_READ_PASS')
        }
    }
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.14.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.14.0'
    implementation 'com.amazonaws:aws-java-sdk-dynamodb:1.12.340'
    implementation 'com.nashss.se.shared-java:test-infrastructure:1.+'
    implementation 'com.nashss.se.shared-java:nss-resources:1.+'
    implementation 'com.nashss.se.shared-java:lesson-plan-resources:1.+'
    implementation 'com.nashss.se.shared-java:java-project-resources:1.+'
}

test {
    useJUnitPlatform()
    jacoco {

        destinationFile = file("$buildDir/jacoco/${name}.exec")

    }
}

checkstyle {
    toolVersion '8.17'

    // Expect the checkstyle config to be in a either the current directory or
    //  a parent, grandparent or great-grandparent directory of the root project
    // NOTE: The purpose of putting checkstyle config in a higher directory is to
    //       share the same config among multiple projects.
    def configFilePath = 'checkstyle/checkstyle.xml'
    for (int attempts = 0; attempts < 4; attempts++) {
        configFile = rootProject.file(configFilePath)

        if (configFile.exists()) {
            break;
        }

        configFilePath = '../' + configFilePath
    }

    def checkstyleDir = configFile.toPath().parent
    def suppressionFile = new File("$checkstyleDir/unit3/suppressions.xml")
    configProperties.put('checkstyle.suppression.filter', suppressionFile) // This property is refereced in `checkstyle.xml`

    sourceSets = [sourceSets.main]
}

// If we are running the task 'staticchecks', do not ignore failures, otherwise ignore
//checkstyleMain.doFirst {
//    //ignoreFailures = !gradle.taskGraph.allTasks.any({ it.name ==~ /.*staticchecks/ })
//    ignoreFailures = true
//}

check.dependsOn jacocoTestCoverageVerification
jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                //minimum = 0.8
                minimum = 0.0
            }
        }
    }
}